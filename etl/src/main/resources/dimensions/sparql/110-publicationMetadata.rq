PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/> 
PREFIX osrap:    <http://vivo.deffopera.dk/ontology/osrap/>
PREFIX abox:     <http://vivo.deffopera.dk/individual/>

CONSTRUCT {
    ?x bibo:doi ?doi .
    ?x bibo:pmid ?pmid .
    ?x vivo:pmcid ?pmcid .
    ?x vivo:identifier ?id .
    ?x rdfs:label ?title .
    ?x bibo:volume ?volume .
    ?x bibo:issue ?issue .
    ?x g:publication_id ?id .
    ?x osrap:authorsCount ?authorsCount .
    ?x osrap:timesCited ?timesCited .
    ?x osrap:relativeCitationRatio ?relativeCitationRatio .
    ?x osrap:fieldCitationRatio ?fieldCitationRatio .
    ?x osrap:matchingstatus ?matchingstatus .
    ?x osrap:relatedToOrganization ?org .
    ?x obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Kind .
    ?vcard vcard:hasURL ?url .
    ?url a vcard:URL .
    ?url vcard:url ?linkoutUrl
} WHERE {
    ?x g:title ?title .
    ?x g:id ?id 
    OPTIONAL {
      ?x g:doi ?doi .
    }
    OPTIONAL {
      ?x g:pmid ?pmid .
    }
    OPTIONAL {
      ?x g:pmcid ?pmcid .
    }
    OPTIONAL {
      ?x g:volume ?volume .
    }
    OPTIONAL {
      ?x g:issue ?issue .
    }
    OPTIONAL {
      ?x g:times_cited ?timesCited 
    }
    OPTIONAL {
      ?x g:relative_citation_ratio ?relativeCitationRatio
    }
    OPTIONAL {
      ?x g:field_citation_ratio ?fieldCitationRatio
    }
    OPTIONAL {
      ?meta g:raw ?x .
      ?root g:meta ?meta .
      ?root g:only-via-grid "true" .
      BIND("Only via GRID" AS ?matchingstatus)
    }
    OPTIONAL {
      ?meta g:raw ?x .
      ?root g:meta ?meta .
      ?root g:dimension-via-grid "true" .
      BIND("Dimensions via GRID" AS ?matchingstatus)
    }
    OPTIONAL {
      ?meta g:raw ?x .
      ?root g:meta ?meta .
      ?root g:dimension-via-ddf-doi "true" .
      BIND("Dimensions via DDF DOI" AS ?matchingstatus)
    }
    OPTIONAL {
      ?x g:ddfGrids ?grid .
      BIND(IRI(CONCAT(STR(abox:), ?grid)) AS ?org)
    }
    OPTIONAL {
      ?x g:linkout ?linkout .
      BIND(IRI(CONCAT(STR(?x), "-vcard")) AS ?vcard)
      BIND(IRI(CONCAT(STR(?x), "-vcard-url")) AS ?url)
      BIND(xsd:anyURI(?linkout) AS ?linkoutUrl)
    }
    OPTIONAL {
      ?x g:authors_count ?authorsCount
    }
}
