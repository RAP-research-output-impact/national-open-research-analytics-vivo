PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/> 
PREFIX osrap:    <http://vivo.deffopera.dk/ontology/osrap/> 

CONSTRUCT {
    ?authorship a vivo:Authorship .
    ?x vivo:relatedBy ?authorship .
    ?authorship vivo:relates ?x .
    ?authorship vivo:relates ?person .
    ?authorship vivo:rank ?rank .
    ?person vivo:relatedBy ?authorship .
    ?person a foaf:Person .
    ?person ?authorP ?authorO .
    ?person g:person_researcher_id ?researcherId .
    ?authorship osrap:relatedToOrganization ?affiliation .
    ?affiliation osrap:relatedByAuthorship ?authorship .
} WHERE {
    ?x g:authors ?author .
    ?author g:authorRank ?position .
    ?author g:affiliations ?affiliation .
    ?author ?authorP ?authorO .
    OPTIONAL {
      ?author g:researcher_id ?researcherId 
    }
    FILTER(?authorP != vitro:position)
    BIND(xsd:integer(?position) AS ?rank)
    BIND(IRI(CONCAT(STR(?x), "-authorship-", ?position)) AS ?authorship)
    BIND(IRI(CONCAT(STR(?x), "-author-", ?position)) AS ?person)
}
