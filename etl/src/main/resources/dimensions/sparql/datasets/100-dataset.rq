PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/> 
PREFIX osrap:    <http://vivo.deffopera.dk/ontology/osrap/>
PREFIX abox:     <http://vivo.deffopera.dk/individual/>

CONSTRUCT {
    ?dataset cito:isCitedAsDataSourceBy ?pub .
    ?dataset a vivo:Dataset .
    ?dataset rdfs:label ?title .
    ?dataset bibo:abstract ?abstract .
    ?dataset bibo:doi ?doi .
    ?dataset vivo:dateTimeValue ?dtv .
    ?dtv a vivo:DateTimeValue .
    ?dtv vivo:dateTime ?dt .
    ?dtv vivo:dateTimePrecision vivo:yearPrecision .
    ?dataset obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Kind .
    ?vcard vcard:hasURL ?url .
    ?url a vcard:URL .
    ?url vcard:url ?linkoutUrl .
    ?dataset vivo:hasPublicationVenue ?repository .
    ?repository a bibo:Collection .
    ?repository rdfs:label ?repositoryId .
} WHERE {
    ?dataset g:dbname "datasets" .
    ?dataset g:id ?id .
    ?dataset g:title ?title .
    OPTIONAL {
        ?dataset g:doi ?doi
    }
    OPTIONAL {
        ?dataset g:description ?abstract 
    }
    OPTIONAL {
        ?dataset g:repository_id ?repositoryId .
	BIND(IRI(CONCAT(STR(abox:), "dataset-respository-", REPLACE(STR(?repositoryId), "\\W", "-"))) AS ?repository)
    }
    OPTIONAL {
        ?dataset g:id ?id .
        ?dataset g:year ?year .
	BIND(xsd:dateTime(CONCAT(STR(?year), "-01-01T00:00:00")) AS ?dt)
	BIND(IRI(CONCAT(STR(?dataset), "-dtv")) AS ?dtv)
    }
    OPTIONAL {
        ?dataset g:figshare_url ?linkout .
        ?dataset g:id ?id .
	BIND(IRI(CONCAT(STR(?dataset), "-vcard")) AS ?vcard) 
	BIND(IRI(CONCAT(STR(?dataset), "-vcard-url")) AS ?url)
	BIND(xsd:anyURI(?linkout) AS ?linkoutUrl)
    }
    OPTIONAL {
        ?dataset g:publication_ids ?resultingPubId .
	BIND(IRI(CONCAT(STR(abox:), ?resultingPubId)) AS ?pub)
    }
    OPTIONAL {
        ?dataset g:associated_publication_id ?resultingPubId .
	BIND(IRI(CONCAT(STR(abox:), ?resultingPubId)) AS ?pub)
    }
}
