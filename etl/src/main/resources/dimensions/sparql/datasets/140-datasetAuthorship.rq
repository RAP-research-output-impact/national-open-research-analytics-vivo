PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/> 
PREFIX osrap:    <http://vivo.deffopera.dk/ontology/osrap/> 
PREFIX abox:     <http://vivo.deffopera.dk/individual/>

CONSTRUCT {
    ?authorship a vivo:Authorship .
    ?x vivo:relatedBy ?authorship .
    ?authorship vivo:relates ?x .
    ?authorship vivo:relates ?person .
    ?authorship vivo:rank ?rank .
    ?person vivo:relatedBy ?authorship .
    ?person a foaf:Person .
    ?authorship osrap:relatedToOrganization ?affiliationOrg .
    ?affiliationOrg osrap:relatedByAuthorship ?authorship .
    ?person g:orcid ?orcid .
    ?person obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Individual .
    ?vcard vcard:hasName ?name .
    ?name a vcard:Name .
    ?name vcard:familyName ?lastName .
    ?name vcard:givenName ?firstName .
    ?affiliationOrg a foaf:Organization .
    ?affiliationOrg rdfs:label ?affiliationName .    
} WHERE {
    ?x g:researchers ?author .
    ?author g:authorRank ?position .
    OPTIONAL {
      ?author g:id ?researcherId 
    }
    OPTIONAL {
      ?author g:orcid ?orcid .
    }
    ?author g:last_name ?lastName .
    ?author g:first_name ?firstName .
    OPTIONAL {
      ?author g:research_orgs ?grid .
      FILTER NOT EXISTS {
          ?author g:research_orgs ?grid2 .
          FILTER(?grid2 != ?grid)
      }
      ?researchOrg g:id ?grid .
      ?x g:research_orgs ?researchOrg .
      ?researchOrg g:name ?affiliationName
      BIND(IRI(CONCAT(STR(abox:), STR(?grid))) AS ?affiliationOrg)
    }
    # If there is no explicit affiliation, but there a research_org listed
    # and no other researchers listed, assume that the researcher is affiliated
    # with that research_org.
    OPTIONAL {
      FILTER NOT EXISTS { ?author g:research_orgs ?someAffiliation }
      FILTER NOT EXISTS { 
          ?x g:researchers ?author2 .
          FILTER(?author2 != ?author)
      }
      ?x g:research_orgs ?researchOrg .
      FILTER NOT EXISTS {
          ?researchOrg g:research_orgs ?researchOrg2 .
          FILTER (?researchOrg2 != ?researchOrg)
      }
      ?researchOrg g:id ?grid .
      ?x g:research_orgs ?researchOrg .
      ?researchOrg g:name ?affiliationName
      BIND(IRI(CONCAT(STR(abox:), STR(?grid))) AS ?affiliationOrg)
    }
    BIND(xsd:integer(?position) AS ?rank)
    BIND(IRI(CONCAT(STR(?x), "-authorship-", ?position)) AS ?authorship)
    BIND(IRI(CONCAT(STR(abox:), STR(?researcherId))) AS ?person)
    BIND(IRI(CONCAT(STR(abox:), STR(?researcherId), "-vcard")) AS ?vcard)
    BIND(IRI(CONCAT(STR(abox:), STR(?researcherId), "-vcard-name")) AS ?name)
}
