PREFIX rdf:      <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:     <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:      <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:      <http://www.w3.org/2002/07/owl#>
PREFIX swrl:     <http://www.w3.org/2003/11/swrl#>
PREFIX swrlb:    <http://www.w3.org/2003/11/swrlb#>
PREFIX vitro:    <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo:     <http://purl.org/ontology/bibo/>
PREFIX c4o:      <http://purl.org/spar/c4o/>
PREFIX cito:     <http://purl.org/spar/cito/>
PREFIX event:    <http://purl.org/NET/c4dm/event.owl#>
PREFIX fabio:    <http://purl.org/spar/fabio/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX geo:      <http://aims.fao.org/aos/geopolitical.owl#>
PREFIX obo:      <http://purl.obolibrary.org/obo/>
PREFIX ocrer:    <http://purl.org/net/OCRe/research.owl#>
PREFIX ocresd:   <http://purl.org/net/OCRe/study_design.owl#>
PREFIX skos:     <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard:    <http://www.w3.org/2006/vcard/ns#>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo:     <http://vivoweb.org/ontology/core#>
PREFIX scires:   <http://vivoweb.org/ontology/scientific-research#>
PREFIX g:        <http://ingest.mannlib.cornell.edu/generalizedXMLtoRDF/0.1/> 
PREFIX osrap:    <http://vivo.deffopera.dk/ontology/osrap/> 
PREFIX abox:     <http://vivo.deffopera.dk/individual/>

CONSTRUCT {
    ?authorship a vivo:Authorship .
    ?x vivo:relatedBy ?authorship .
    ?authorship vivo:relates ?x .
    ?authorship vivo:relates ?person .
    ?authorship vivo:rank ?rank .
    ?person vivo:relatedBy ?authorship .
    ?person a foaf:Person .
    ?person rdfs:label ?personLabel .
    ?person obo:ARG_2000028 ?vcard .
    ?vcard a vcard:Individual .
    ?vcard vcard:hasName ?name .
    ?name a vcard:Name .
    ?name vcard:familyName ?lastName .
    ?name vcard:givenName ?firstName .
} WHERE {
    ?x g:inventors ?author .
    ?author g:authorRank ?rank .
    ?author g:name ?authorFullName .
    ?author g:uncontrolledName ?authorUncontrolledName .
    BIND(IRI(CONCAT(STR(?x), "-authorship", STR(?rank))) AS ?authorship)
    BIND(IRI(CONCAT(STR(?x), "-author", STR(?rank))) AS ?uncontrolledAuthorInd)
    OPTIONAL {
      ?x g:researchers ?researcher .
      ?researcher g:last_name_lower ?researcherLastNameLower .
      FILTER(REGEX(?authorFullName, ?researcherLastNameLower))
      FILTER NOT EXISTS {
        ?x g:researchers ?researcher2 .
        ?researcher2 g:last_name_lower ?researcher2LastNameLower .
        FILTER(REGEX(?authorFullName, ?researcher2LastNameLower))
	FILTER(?researcher != ?researcher2)
      }
      ?researcher g:id ?researcherId .
      ?researcher g:last_name ?lastName .
      ?researcher g:first_name ?firstName .
      BIND(CONCAT(STR(?lastName), ", ", STR(?firstName)) AS ?controlledPersonLabel) 
      BIND(IRI(CONCAT(STR(abox:), ?researcherId)) AS ?controlledAuthorInd)
      BIND(IRI(CONCAT(STR(abox:), ?researcherId, "-vcard")) AS ?vcard)
      BIND(IRI(CONCAT(STR(abox:), ?researcherId, "-vcard-name")) AS ?name)
    }
    BIND(COALESCE(?controlledAuthorInd, ?uncontrolledAuthorInd) AS ?person)
    BIND(COALESCE(?controlledPersonLabel, ?authorUncontrolledName) AS ?personLabel)
}
